reset
# ----------------------------------------------------------------------
# Set units to SI.
# ----------------------------------------------------------------------
#{Units('si')}

##################################################################
${domainX=2000*km}
${domainY=1500*km}
${domainZ=400*km}

${domainCenterX=0.5*domainX}
${domainCenterY=0.5*domainY}
${domainCenterZ=-0.5*domainZ}

# Create the domain and move it so that origin will be at the top southwestern corner.
Brick X {domainX} Y {domainY} Z {domainZ}
move volume 1 X {domainCenterX} Y {domainCenterY} Z {domainCenterZ}

# Cut the domain with a flat Moho at 35 km depth and an LAB at 100 km depth
webcut body 1 with plane zplane offset {-35*km}
webcut body 2 with plane zplane offset {-100*km}

# create the rupture plane box
##### Values a user need to provide #####
### Note that km length units are implied.
#{faultLength=500.0*km}
#{faultWidth =200.0*km}
#{faultHtop  =5.37*km}
#{faultDip   =12.0}
#{faultStrike=200.0}
### Epicenter x and y coordinates are converted from the lat lon
### of the epicenter to the model coordinates.
### See https://osf.io/6szrn/wiki/Defining%20boundary%20surfaces%20and%20fault%20plane/
#{epicenterX=1500.0*km}
#{epicenterY=800.0*km}
### Depth to the hypocenter
#{hypocenterDepth=-22.0*km}
### Hypocenter x and y coordinates rel.to the fault plane origin (top left corner)
#{hypocenterX=190.0*km}
#{hypocenterZ=80.0*km}

##### Derived values #####
#{blockLength=faultLength+0.0}
#{blockWidth =faultWidth*cos(faultDip)+0.0}
#{blockHeight=110*km}

#{faultOriginX=epicenterX+hypocenterZ*cos(faultDip)}
#{faultOriginY=epicenterY+hypocenterX}

#{domain_center_x=faultOriginX - 0.5*blockWidth}
#{domain_center_y=faultOriginY - 0.5*blockLength}
#{domain_center_z=-0.5*blockHeight+10.0*km}

##### Create and recenter a box containing the fault plane.
brick x {blockWidth} y {blockLength} z {blockHeight}
volume 4 move x {domain_center_x} y {domain_center_y} z {domain_center_z}

##### Create the fault plane
create planar surface with plane zplane offset {-22*km}
rotate Surface 33 angle {faultDip} about origin {epicenterX} {epicenterY} {hypocenterDepth} direction 0 -1 0 include_merged

##### Rotate the volumes to achieve the fault strike
rotate vol 4 angle {180-faultStrike} about origin {epicenterX} {epicenterY} {hypocenterDepth} direction 0 0 1 include_merged
rotate Surface 33 angle {180-faultStrike} about origin {epicenterX} {epicenterY} {hypocenterDepth} direction 0 0 1 include_merged

##### Cut the box with the fault plane
webcut vol 4 with sheet extended from surface 33

##### Delete
delete surf 33
delete vol 6

# Cut crust and lithosphere with the hangingwall
chop vol 1 with vol 4 keep
chop vol 2 with vol 4 keep

# clean up
delete vol 1 2 4
imprint all with all
merge all
compress all

##################################################################
# ----------------------------------------------------------------------
# Assign sizes to different regions and define meshing scheme.
# ----------------------------------------------------------------------
#{min_size=5.0*km}
#{int_size=20.0*km}
#{max_size=50.0*km}

volume all size {max_size}
volume 2 size {min_size}
volume 4 size {min_size}

volume all scheme tetmesh

# ----------------------------------------------------------------------
# Generate the mesh.
# ----------------------------------------------------------------------
mesh surface all
mesh volume all

# ----------------------------------------------------------------------
# Smooth the mesh to improve quality.
# ----------------------------------------------------------------------

# Loop where we decrease desired condition number at each step.
#{condnum=2.4}
#{loop(2)}
volume all smooth scheme condition number beta {condnum} cpu 5
smooth volume all
#{condnum=condnum-0.1}
#{endloop}

##################################################################


# ----------------------------------------------------------------------
# Create blocks for different materials.
# ----------------------------------------------------------------------
block 1 vol 2 3
block 1 name "crust"

block 2 vol 4 5
block 2 name "lithosphere"

block 3 vol 1
block 3 name "asthenosphere"

# ----------------------------------------------------------------------
# Create nodeset for dike and dike edge.
# ----------------------------------------------------------------------
group "fault" add node in surface 9 22
nodeset 100 group fault
nodeset 100 name "fault"

group "fault_edge" add node in curve 14 22 41 43 44
nodeset 102 group fault_edge
nodeset 102 name "fault_edge"

# ----------------------------------------------------------------------
# Create nodesets for all external boundaries.
# ----------------------------------------------------------------------
group "face_xpos" add node in surface 4 14 25
nodeset 10 group face_xpos
nodeset 10 name "face_xpos"

group "face_xneg" add node in surface 6 17 27
nodeset 11 group face_xneg
nodeset 11 name "face_xneg"

group "face_ypos" add node in surface 3 18 24
nodeset 12 group face_ypos
nodeset 12 name "face_ypos"

group "face_yneg" add node in surface 5 15 26
nodeset 13 group face_yneg
nodeset 13 name "face_yneg"

group "face_zpos" add node in surface 13 16
nodeset 14 group face_zpos
nodeset 14 name "face_zpos"

group "face_zneg" add node in surface 1
nodeset 15 group face_zneg
nodeset 15 name "face_zneg"

# ----------------------------------------------------------------------
# Export exodus file.
# ----------------------------------------------------------------------
set large exodus off
export mesh "./korea_simple.exo" dimension 3 overwrite

# End of file
